plugins {
    id 'net.researchgate.release' version '2.3.4'
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'net.researchgate.archaius'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.netflix.archaius:archaius-core:0.7.3'
    compile 'com.ecwid.consul:consul-api:1.1.7'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    testCompile 'org.powermock:powermock-api-mockito:1.6.3'
    testCompile 'org.powermock:powermock-core:1.6.3'
    testCompile 'org.powermock:powermock-module-junit4:1.6.3'
}

bintray {
    dryRun = false //Whether to run this as dry-run, without deploying
    publish = false //If version should be auto published after an upload

    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    configurations = ['archives']
    pkg {
        name = 'archaius-consul'
        repo = 'maven'
        userOrg = 'researchgate'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/researchgate/archaius-consul.git'
        issueTrackerUrl = 'https://github.com/researchgate/archaius-consul/issues'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}
